plugins {
	id 'java-library'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version "7.0.0"
}

repositories {
	mavenCentral()
	maven {
		url "https://repo.spongepowered.org/maven/"
	}
}

sourceCompatibility = '9'
targetCompatibility = '9'

version = "0.0.0.0"
group = "com.wildermods"
archivesBaseName = "wilderforge"

dependencies {

	shadow group: 'org.apache.commons', name: 'commons-text', version: '1.9'
	shadow group: 'org.jgrapht', name: 'jgrapht-core', version: '1.5.1'
	shadow group: 'net.oneandone.reflections8', name: 'reflections8', version: '0.11.5'
	shadow group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '6.0-alpha-3'

	//mixin requirements:
	shadow "cpw.mods:modlauncher:" + modLauncherVersion
	shadow 'cpw.mods:grossjava9hacks:1.1.+'
	shadow 'org.spongepowered:mixin:' + mixinVersion
	shadow 'com.google.guava:guava:21.0'
	shadow group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
	shadow group: 'org.ow2.asm', name: 'asm', version: asmVersion
	shadow group: 'org.ow2.asm', name: 'asm-util', version: asmVersion
	shadow group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
	shadow group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
	
	//JUNIT TESTS
	testImplementation 'junit:junit:4.13'
	testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
	testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'


	/**********
	don't alter the dependencies below
	***********/
	
	//Wildermyth, Scratchpad
	implementation fileTree(dir: './bin/', include: ['*.jar'])
	
	//All dependencies of Wildermyth itself
	implementation fileTree(dir: './bin/lib/', include: ['*.jar'])
}

test {
	testLogging.showStandardStreams = true
}

shadowJar {
	mergeServiceFiles()
	configurations = [project.configurations.shadow]
	exclude 'META-INF/LICENSE'
	exclude 'META-INF/LICENSE.txt'
	exclude 'META-INF/LICENSsE.txt'
	exclude 'META-INF/NOTICE'
	exclude 'META-INF/NOTICE.txt'
	exclude 'META-INF/DEPENDENCIES'
	exclude 'LICENSE.txt'
}


eclipse {
	classpath {
		file {
			whenMerged {
				def decomp = fileReference(file('bin/decomp'))
				def lib = entries.find { it.path.contains 'wildermyth.jar'}
				lib.sourcePath = decomp
				lib = entries.find { it.path.contains 'scratchpad.jar'}
				lib.sourcePath = decomp
				lib = entries.find { it.path.contains 'server-1.0.jar'}
				lib.sourcePath = decomp
				lib = entries.find { it.path.contains 'gameEngine-1.0.jar'}
				lib.sourcePath = decomp
			}
		}
	}
}

jar {
	manifest {
		attributes(
			'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
			'MixinConfigs': 'mixins.json',
			'Main-Class': 'cpw.mods.modlauncher.Launcher',
			'Specification-Version': 8.0,
			'Multi-Release': 'true'
		)
	}
}

build.finalizedBy shadowJar